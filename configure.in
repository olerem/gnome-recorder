AC_INIT(gmix)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-media, 0.30)
AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT

AC_PROG_CC
AC_ISC_POSIX

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Look for some version of curses (keeping AM_'s out of curses.m4)
AC_CHECK_CURSES
AM_CONDITIONAL(HAS_CURSES, test "$has_curses" = true)

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/ioctl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MMAP
AC_CHECK_FUNCS(getcwd getwd putenv strdup strerror)
AM_PROG_LIBTOOL

ALL_LINGUAS="ko de fr it ja es no ga pt ru_RU"
AM_GNU_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl extace
dnl
PROGRAMS_EXTACE=""
AC_CHECK_LIB(esd,main,[
  AC_CHECK_LIB(gslfft,gsl_fft_real,PROGRAMS_EXTACE=extace,,-lm -lgslerr)],,$GNOME_LIBS $LIBS -lm)
AC_SUBST(PROGRAMS_EXTACE)

dnl
dnl gmix
dnl
PROGRAMS_GMIX=""
AC_CHECK_HEADERS(linux/soundcard.h,[PROGRAMS_GMIX=gmix],[
  AC_CHECK_HEADERS(machine/soundcard.h, [PROGRAMS_GMIX=gmix])])
AC_SUBST(PROGRAMS_GMIX)

dnl
dnl Find out if we can compile gnomovision on this system
dnl
PROGRAMS_GNOMOVISION=""
AC_CHECK_HEADERS(linux/videodev.h,[PROGRAMS_GNOMOVISION=gnomovision])
AC_SUBST(PROGRAMS_GNOMOVISION)

dnl
dnl Everything OK for tcd?
dnl
PROGRAMS_TCD=""
AC_CHECK_HEADERS(linux/cdrom.h,[PROGRAMS_TCD=tcd])

AC_MSG_CHECKING([whether to use cd changer support (Linux 2.1.x headers required)])
AC_EGREP_CPP(answer_affirmatively,
[#include <linux/cdrom.h>
 #ifdef CDROM_CHANGER_NSLOTS
	answer_affirmatively
 #endif
], cd_changer=yes, cd_changer=no)
if test "$cd_changer" = "yes"; then
	USE_CD_CHANGER="-DTCD_CHANGER_ENABLED"
fi
AC_MSG_RESULT("$cd_changer")

AC_SUBST(USE_CD_CHANGER)
AC_SUBST(PROGRAMS_TCD)

PROGRAMS_CDDBSLAVE=""
AC_SUBST(PROGRAMS_CDDBSLAVE)

AC_OUTPUT([
Makefile
po/Makefile.in
dnl extace/Makefile
gmix/Makefile
dnl gnomovision/Makefile
intl/Makefile
macros/Makefile
tcd/Makefile
tcd/icons/Makefile
cddbslave/Makefile
version.h
stamp.h],[sed -e "/POTFILES =/r po/POTFILES" po/Makefile.in > po/Makefile])
