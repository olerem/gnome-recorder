AC_INIT(gmix)

AM_CONFIG_HEADER(config.h)
AM_INIT_AUTOMAKE(gnome-media, 0.99.8)
AM_MAINTAINER_MODE

AM_ACLOCAL_INCLUDE(macros)

GNOME_INIT

AC_PROG_CC
AC_ISC_POSIX

dnl Checks for programs.
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_MAKE_SET

dnl Look for some version of curses (keeping AM_'s out of curses.m4)
AC_CHECK_CURSES
AM_CONDITIONAL(HAS_CURSES, test "$has_curses" = true)

dnl Utility conditional
AM_CONDITIONAL(FALSE, test x = y)

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h limits.h malloc.h strings.h sys/ioctl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_C_INLINE
AC_TYPE_OFF_T
AC_TYPE_SIZE_T

dnl Checks for library functions.
AC_FUNC_ALLOCA
AC_PROG_GCC_TRADITIONAL
AC_FUNC_MMAP
AC_CHECK_FUNCS(getcwd getwd putenv strdup strerror)
AM_PROG_LIBTOOL

dnl
dnl Check for esound library
dnl
AM_PATH_ESD(0.2.7, AC_DEFINE(HAVE_ESD))


ALL_LINGUAS="da fi ko de fr it ja es no ga pt ru_RU nl"
AM_GNOME_GETTEXT

AC_SUBST(CFLAGS)
AC_SUBST(CPPFLAGS)
AC_SUBST(LDFLAGS)

dnl
dnl extace
dnl
PROGRAMS_EXTACE=""
AC_CHECK_LIB(gslfft, gsl_fft_real_radix2,
	PROGRAMS_EXTACE=extace,,-lgslfft -lgslerr -lm)
AC_CHECK_HEADERS(gsl/gsl.h, [USE_GSL_GSL_H=1])
AM_CONDITIONAL(BUILD_EXTACE, test "$PROGRAMS_EXTACE" = "extace")

dnl
dnl vu-meter
dnl
PROGRAMS_VUMETER=""
AC_CHECK_LIB(esd,main,PROGRAMS_VUMETER=vu-meter,,[$ESD_LIBS])
AC_SUBST(PROGRAMS_VUMETER)

dnl
dnl gmix
dnl
PROGRAMS_GMIX=""
AC_CHECK_HEADERS(linux/soundcard.h,[PROGRAMS_GMIX=gmix],[
  AC_CHECK_HEADERS(machine/soundcard.h, [PROGRAMS_GMIX=gmix])])
AC_SUBST(PROGRAMS_GMIX)

dnl
dnl Find out if we can compile gnomovision on this system
dnl [disabled]
dnl
dnl PROGRAMS_GNOMOVISION=""
dnl AC_CHECK_HEADERS(linux/videodev.h,[PROGRAMS_GNOMOVISION=gnomovision])
dnl AC_SUBST(PROGRAMS_GNOMOVISION)
dnl AC_CHECK_LIB(Xxf86dga, XF86DGAQueryExtension,[GNOMOVISION_LIBS=-lXxf86dga],,[$GNOME_LIBDIR $GNOMEUI_LIBS])
dnl AC_SUBST(GNOMOVISION_LIBS)

dnl
dnl Everything OK for tcd?
dnl
PROGRAMS_TCD=""
AC_CHECK_HEADERS(linux/cdrom.h,[PROGRAMS_TCD=tcd])
AC_CHECK_HEADERS(sys/cdio.h,[PROGRAMS_TCD=tcd])
AC_CHECK_HEADERS(sun/dkio.h,[PROGRAMS_TCD=tcd])

AC_MSG_CHECKING([whether to use cd changer support (Linux 2.1.x headers required)])
AC_EGREP_CPP(answer_affirmatively,
[#include <linux/cdrom.h>
 #ifdef CDROM_CHANGER_NSLOTS
	answer_affirmatively
 #endif
], cd_changer=yes, cd_changer=no)
if test "$cd_changer" = "yes"; then
	USE_CD_CHANGER="-DTCD_CHANGER_ENABLED"
fi
AC_MSG_RESULT("$cd_changer")

AC_SUBST(USE_CD_CHANGER)
AC_SUBST(PROGRAMS_TCD)

AC_OUTPUT([
Makefile
po/Makefile.in
extace/Makefile
gmix/Makefile
vu-meter/Makefile
dnl gnomovision/Makefile
macros/Makefile
tcd/Makefile
tcd/icons/Makefile
cddbslave/Makefile
version.h])
